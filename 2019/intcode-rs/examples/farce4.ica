# toys from a FORTH?

# some "indirect threaded" code...

rel $stack

add $0, ip, _j1
add $0, _j1: 0, _j2
add $1, ip, ip
jz $0, _j2: 0

ip: $thread

thread: $lit
$7
$lit
$3
$quadruple
$double
$plus
$print
$_halt

quadruple: $docol
$double
$double
$exit

double: $docol
$dup
$plus
$exit

print: $print_code
print_code: out (0)
rel $-1
jz $0, $_next

plus: $plus_code
plus_code: add (-1), (0), (-1)
rel $-1
jz $0, $_next

lit: $lit_code
lit_code: rel $1
add $0, ip, lit_src
add $0, lit_src: 0, (0)
add $1, ip, ip
jz $0, $_next

dup: $dup_code
dup_code: rel $1
add $0, (-1), (0)
jz $0, $_next

docol: add $0, rstack_bp, docol_push_target
add $0, ip, docol_push_target: 0
add $1, rstack_bp, rstack_bp
add $-1, ip, old_ip
add $1, old_ip: 0, ip
jz $0, $_next

exit: $exit_code
exit_code: add $-1, rstack_bp, rstack_bp
add $0, rstack_bp, exit_pop_src
add $0, exit_pop_src: 0, ip
jz $0, $_next

_next: add $0, ip, _jnext1
add $0, _jnext1: 0, _jnext2
add $1, ip, ip
jz $0, _jnext2: 0

_halt: $halt_code
halt_code: hlt

# assumption: data stack will be larger than return stack
# return stack will have a max capacity of 16
rstack_bp: $rstack
rstack: $0
$0
$0
$0
$0
$0
$0
$0
$0
$0
$0
$0
$0
$0
$0
$0
stack: $0
